@tokens {
  space { @whitespace+ }

  Comment { "#" ![\n]* }

  identifier { $[a-zA-Z_] $[a-zA-Z_0-9]* }

  number { $[0-9]+ ("." $[0-9]+)? }
  string { "\"" (!["\\] | "\\" (![.] | $[.]))* "\"" }
  pattern { "[" $[ \t\n\rx-]* "]" }

  "="
  ":"
  "{" "}"
  "(" ")"
  ","

  "<<"
}

@skip { space | Comment }

@detectDelim

@top Program {
  (Assignment | Routing | NamedBlock)*
}

Property {
  PropertyName ":" Literal
}

PropertyName { identifier }

Literal {
  NumberLiteral | StringLiteral | PatternLiteral
}

NumberLiteral { number }
StringLiteral { string }
PatternLiteral { pattern }

Assignment {
  AssignmentName "=" (Literal | Call | VariableReference)
}

AssignmentName { identifier }

VariableReference { identifier }

Call {
  Callee "(" (Property ("," Property)*)? ")"
}

Callee { identifier }

Routing {
  RoutingTarget "<<" (PatternLiteral | VariableReference)
}

RoutingTarget { identifier }

NamedBlock {
  BlockName Block
}

BlockName { identifier }

Block {
  "{"
  (Property)*
  "}"
}
