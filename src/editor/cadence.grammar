@tokens {
  space { @whitespace+ }

  Comment { "#" ![\n]* }

  word { $[a-zA-Z_] $[a-zA-Z_0-9]* }

  number { $[0-9]+ ("." $[0-9]+)? }
  string { "\"" (!["\\] | "\\" (![.] | $[.]))* "\"" }
  pattern { "[" $[ \t\n\rx-]* "]" }

  "="
  ":"
  "{" "}"
  "(" ")"
  ","

  "<<"
}

@skip { space | Comment }

@detectDelim

kw<term> { @specialize[@name={term}]<word, term> }

@top Program {
  (Assignment | TrackStatement)*
}

unit {
  @specialize<word, "bpm" | "bars" | "beats" | "s" | "ms" | "hz" | "db">
}

NumberLiteral { number unit? }
StringLiteral { string }
PatternLiteral { pattern }

Literal {
  NumberLiteral | StringLiteral | PatternLiteral
}

VariableDefinition { word }
VariableName { word }

Property {
  PropertyName { word }
  ":"
  Literal
}

Assignment {
  VariableDefinition
  "="
  (Literal | Call | VariableName)
}

Call {
  Callee { word }
  "(" (Property ("," Property)*)? ")"
}

Routing {
  VariableName
  "<<"
  (PatternLiteral | VariableName)
}

TrackStatement {
  kw<"track">
  TrackBlock {
    "{" (Property | SectionStatement)* "}"
  }
}

SectionStatement {
  kw<"section"> VariableDefinition
  kw<"for"> NumberLiteral
  SectionBlock {
    "{" (Routing)* "}"
  }
}
