@tokens {
  space { @whitespace+ }

  Comment { "//" ![\n]* }

  word { $[a-zA-Z_] $[a-zA-Z_0-9#]* }

  number { $[0-9]+ ("." $[0-9]+)? }
  string { "\"" (!["\\] | "\\" (![.] | $[.]))* "\"" }

  "{" "}"
  "(" ")"
  ","

  "="
  ":"
  "<<"

  "+"
  "-"
  "*"
  "/"

  @precedence { Comment, "/" }
}

@skip { space | Comment }

@detectDelim

kw<term> { @specialize[@name={term}]<word, term> }

@precedence {
  multiply @left,
  plus @left
}

@top Program {
  (Assignment | TrackStatement | MixerStatement)*
}

unit {
  @extend<word, "bpm" | "bars" | "beats" | "s" | "ms" | "hz" | "db">
}

VariableDefinition { word }
VariableName { word }

NumberLiteral { number unit? }
StringLiteral { string }

Literal {
  NumberLiteral | StringLiteral
}

Pattern {
  "["
  ((word | "-") ("(" argumentList? ")")? (":" primary)?)*
  "]"
}

primary {
  "(" expression ")" | Literal | Pattern | Call | VariableName
}

expression {
  primary | UnaryExpression | BinaryExpression
}

UnaryExpression {
  ("+" | "-") expression
}

BinaryExpression {
  (expression !plus "+" expression) |
  (expression !plus "-" expression) |
  (expression !multiply "*" expression) |
  (expression !multiply "/" expression)
}

Property {
  PropertyName { word }
  ":"
  expression
}

Assignment {
  VariableDefinition
  "="
  expression
}

Call {
  Callee { word }
  "(" argumentList? ")"
}

argumentList {
  argument ("," argument)*
}

argument {
  Property | expression
}

Routing {
  VariableName
  "<<"
  (VariableName "<<")*
  expression
}

TrackStatement {
  kw<"track">
  TrackBlock[group=Block] {
    "{" (Property | SectionStatement)* "}"
  }
}

SectionStatement {
  kw<"section"> VariableDefinition
  kw<"for"> expression
  SectionBlock[group=Block] {
    "{" (Property | Routing)* "}"
  }
}

MixerStatement {
  kw<"mixer">
  MixerBlock[group=Block] {
    "{" (Property | Routing | BusStatement)* "}"
  }
}

BusStatement {
  kw<"bus">
  VariableDefinition
  BusBlock[group=Block] {
    "{" (Property | EffectStatement)* "}"
  }
}

EffectStatement {
  kw<"effect">
  expression
}
