@tokens {
  space { @whitespace+ }

  Comment { "//" ![\n]* }

  word { $[a-zA-Z_] $[a-zA-Z_0-9#]* }

  number { $[0-9]+ ("." $[0-9]+)? }

  stringDelimiter { "\"" }
  stringEscape { "\\" $[.] }
  stringContent { !["\\{] }

  "[" "]"
  "<" ">"
  "{" "}"
  "(" ")"
  ","
  "."

  "="
  ":"
  "<<"

  "+"
  "-"
  "*"
  "/"

  @precedence { Comment, "/" }
}

@skip { space | Comment }

@detectDelim

kw<term> { @specialize[@name=keyword]<word, term> }
unit<term> { @specialize[@name=unit]<word, term> }

Unit {
  unit<"bpm"> |
  unit<"bars"> |
  unit<"beats"> |
  unit<"s"> |
  unit<"ms"> |
  unit<"hz"> |
  unit<"db">
}

@precedence {
  multiply @left,
  plus @left
}

@top Program {
  (UseStatement | Assignment | TrackStatement | MixerStatement)*
}

interpolationExpression {
  "{"
  expression
  "}"
}

VariableDefinition { word }
VariableName { word }

Number { number }

@skip {} {
  String {
    stringDelimiter
    (stringEscape | stringContent | interpolationExpression)*
    stringDelimiter
  }
}

Pattern { serialPattern }

serialPattern {
  "["
  (step | serialPattern | parallelPattern | interpolationExpression)*
  "]"
}

parallelPattern {
  "<"
  (step | serialPattern | parallelPattern | interpolationExpression)*
  ">"
}

step {
  (word | "-") ("(" argumentList? ")")? (":" primary)?
}

argumentList {
  (Property | expression)
  ("," (Property | expression))*
}

primary {
  "(" expression ")" | Number | String | Pattern | Call | VariableName
}

Call {
  Callee { word }
  "(" argumentList? ")"
}

accessOrCall {
  primary
  (
    "." Unit |
    "." Call |
    "." MemberAccess { word }
  )*
}

expression {
  accessOrCall | UnaryExpression | BinaryExpression
}

UnaryExpression {
  ("+" | "-") expression
}

BinaryExpression {
  (expression !plus "+" expression) |
  (expression !plus "-" expression) |
  (expression !multiply "*" expression) |
  (expression !multiply "/" expression)
}

Property {
  PropertyName { word }
  ":"
  expression
}

UseStatement {
  kw<"use"> String
  kw<"as"> (word | "*")
}

Assignment {
  VariableDefinition
  "="
  expression
}

Routing {
  VariableName
  "<<"
  (VariableName "<<")*
  expression
}

Automation {
  kw<"automate">
  expression
  kw<"as">
  expression
}

TrackStatement {
  kw<"track">
  ("(" argumentList? ")")?
  TrackBlock[group=Block] {
    "{" (PartStatement)* "}"
  }
}

PartStatement {
  kw<"part"> VariableDefinition
  ("(" argumentList? ")")?
  PartBlock[group=Block] {
    "{" (Routing | Automation)* "}"
  }
}

MixerStatement {
  kw<"mixer">
  ("(" argumentList? ")")?
  MixerBlock[group=Block] {
    "{" (BusStatement)* "}"
  }
}

BusStatement {
  kw<"bus"> VariableDefinition
  ("(" argumentList? ")")?
  BusBlock[group=Block] {
    "{" (VariableName | EffectStatement)* "}"
  }
}

EffectStatement {
  kw<"effect">
  expression
}
