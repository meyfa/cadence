@tokens {
  space { @whitespace+ }

  Comment { "//" ![\n]* }

  word { $[a-zA-Z_] $[a-zA-Z_0-9#]* }

  number { $[0-9]+ ("." $[0-9]+)? }

  stringDelimiter { "\"" }
  stringEscape { "\\" $[.] }
  stringContent { !["\\{] }

  "[" "]"
  "<" ">"
  "{" "}"
  "(" ")"
  ","

  "="
  ":"
  "<<"

  "+"
  "-"
  "*"
  "/"

  @precedence { Comment, "/" }
}

@skip { space | Comment }

@detectDelim

kw<term> { @specialize[@name={term}]<word, term> }

@precedence {
  multiply @left,
  plus @left
}

@top Program {
  (UseStatement | Assignment | TrackStatement | MixerStatement)*
}

unit {
  @extend<word, "bpm" | "bars" | "beats" | "s" | "ms" | "hz" | "db">
}

interpolationExpression {
  "{"
  expression
  "}"
}

VariableDefinition { word }
VariableName { word }

Number { number unit? }

@skip {} {
  String {
    stringDelimiter
    (stringEscape | stringContent | interpolationExpression)*
    stringDelimiter
  }
}

Pattern { serialPattern }

serialPattern {
  "["
  (step | serialPattern | parallelPattern | interpolationExpression)*
  "]"
}

parallelPattern {
  "<"
  (step | serialPattern | parallelPattern | interpolationExpression)*
  ">"
}

step {
  (word | "-") ("(" argumentList? ")")? (":" primary)?
}

primary {
  "(" expression ")" | Number | String | Pattern | Call | VariableName
}

expression {
  primary | UnaryExpression | BinaryExpression
}

UnaryExpression {
  ("+" | "-") expression
}

BinaryExpression {
  (expression !plus "+" expression) |
  (expression !plus "-" expression) |
  (expression !multiply "*" expression) |
  (expression !multiply "/" expression)
}

Property {
  PropertyName { word }
  ":"
  expression
}

UseStatement {
  kw<"use"> String
  kw<"as"> (word | "*")
}

Assignment {
  VariableDefinition
  "="
  expression
}

Call {
  Callee { word }
  "(" argumentList? ")"
}

argumentList {
  (Property | expression)
  ("," (Property | expression))*
}

Routing {
  VariableName
  "<<"
  (VariableName "<<")*
  expression
}

TrackStatement {
  kw<"track">
  TrackBlock[group=Block] {
    "{" (Property | SectionStatement)* "}"
  }
}

SectionStatement {
  kw<"section"> VariableDefinition
  kw<"for"> expression
  SectionBlock[group=Block] {
    "{" (Property | Routing)* "}"
  }
}

MixerStatement {
  kw<"mixer">
  MixerBlock[group=Block] {
    "{" (Property | Routing | BusStatement)* "}"
  }
}

BusStatement {
  kw<"bus">
  VariableDefinition
  BusBlock[group=Block] {
    "{" (Property | EffectStatement)* "}"
  }
}

EffectStatement {
  kw<"effect">
  expression
}
